/**
* Used to manage the local (self) player's profile information.
* 
* This implementation uses a simple delay timer to establish the leader/dealer role.
*
* (C)opyright 2014 to 2017
*
* This source code is protected by copyright and distributed under license.
* Please see the root LICENSE file for terms and conditions.
*
*/

package org.cg.widgets {
	
	import feathers.controls.TextInput;	
	import flash.net.FileFilter;
	import org.cg.events.LoungeEvent;
	import org.cg.interfaces.ILounge;
	import org.cg.interfaces.IPanelWidget;
	import org.cg.SlidingPanel;
	import starling.events.Event;
	import feathers.events.FeathersEventType;
	import feathers.controls.Button;
	import org.cg.GlobalSettings;
	import flash.utils.getDefinitionByName;
	import flash.net.FileReference;
	import flash.net.SharedObject;
	import flash.net.SharedObjectFlushStatus;
	import org.cg.DebugView;	
		
	public class PlayerProfileWidget extends PanelWidget implements IPanelWidget {
		
		private var _iconFileBrowser:*; //File instance used to select and load profile icon image
		//UI components generated by StarlingViewManager:
		public var updatePlayerIconButton:Button;
		public var playerHandleInput:TextInput;
		
		/**
		 * Creates a new instance.
		 * 
		 * @param	loungeRef A reference to the main ILounge implementation instance.
		 * @param	panelRef A reference to the parent or containing SlidingPanel instance.
		 * @param	widgetData The widget's configuration data, usually from the global settings data.
		 */
		public function PlayerProfileWidget(loungeRef:ILounge, panelRef:SlidingPanel, widgetData:XML) {
			DebugView.addText("PlayerProfileWidget created");
			super(loungeRef, panelRef, widgetData);			
		}
		
		/**
		 * Initializes the new instance after it's been added to the display list and all child components have been fully created.
		 */
		override public function initialize():void {
			DebugView.addText("PlayerProfileWidget.initialize");
			this.updatePlayerIconButton.addEventListener(Event.TRIGGERED, this.onUpdatePlayerIconClick);
			if (lounge.currentPlayerProfile.iconLoaded) {
				this.updateProfileInfo(null);
			}
			lounge.addEventListener(LoungeEvent.UPDATED_PLAYERPROFILE, this.updateProfileInfo);
			this.playerHandleInput.addEventListener(FeathersEventType.FOCUS_OUT, this.onPlayerHandleInputLoseFocus);
		}
		
		/**
		 * @return A reference to the "flash.filesystem.File" class if available in the current runtime, or null if unavailable.
		 */
		private static function get File():Class {
			try {
				var fileClass:Class = getDefinitionByName("flash.filesystem.File") as Class;
				return (fileClass);
			} catch (err:*) {				
			}
			return (null);
		}
		
		/**
		 * Event listener invoked when the player icon button has been clicked, opening up a file selection dialog to allow
		 * the user to select a new player icon.
		 * 
		 * @param	eventObj An Event object.
		 */
		private function onUpdatePlayerIconClick(eventObj:Event):void {
			if (this._iconFileBrowser != null) {
				this._iconFileBrowser.removeEventListener("complete", this.onIconFileLoaded);
				this._iconFileBrowser.removeEventListener("select", this.onIconFileSelect);
			}
			var fileFilter:FileFilter = new FileFilter("Image", "*.jpg;*.png;*.gif");			
			if (lounge.settings.systemSettings.isAIR == false) {
				this._iconFileBrowser = new FileReference();
				this._iconFileBrowser.addEventListener("select", this.onIconFileSelect);
				this._iconFileBrowser.browse([fileFilter]);
			} else {
				this._iconFileBrowser = File.desktopDirectory;
				this._iconFileBrowser.addEventListener("select", this.onIconFileSelect);
				this._iconFileBrowser.browseForOpen("Select icon image", [fileFilter]);
			}
		}
		
		/**
		 * Event listener invoked when the file open dialog has been closed with an icon file selected. The path to the newly-selected icon
		 * file is saved to the global settings.
		 * 
		 * @param	eventObj A generic "Event" object (used to prevent namespace collisions with Starling events).
		 */
		private function onIconFileSelect(eventObj:Object):void {
			DebugView.addText("onIconFileSelect");
			this._iconFileBrowser.removeEventListener("select", this.onIconFileSelect);
			if (eventObj.target is FileReference) {
				DebugView.addText ("target is file reference");
				this._iconFileBrowser.addEventListener("complete", this.onIconFileLoaded);
				this._iconFileBrowser.load();
			} else {
				DebugView.addText ("target is NOT file reference");				
				lounge.currentPlayerProfile.profileData.child("icon")[0].replace("*", new XML("<![CDATA[" + this._iconFileBrowser.nativePath + "]]>"));			
				GlobalSettings.saveSettings();
				lounge.currentPlayerProfile.load();
			}
		}
		
		/**
		 * Event listener invoked when the icon file is loaded from a local source by the web runtime. The file is stored in the Local Shared Object
		 * so that it can be recalled by the player profile handler, which would otherwise attempt to load the file from disk.
		 * 
		 * @param	eventObj A generic "Event" object (used to to prevent namespace collisions with Starling events).
		 */
		private function onIconFileLoaded(eventObj:Object):void {
			DebugView.addText ("onIconFileLoaded");
			this._iconFileBrowser.removeEventListener("complete", this.onIconFileLoaded);
			var profileName:String = "playericon_"+ lounge.currentPlayerProfile.profileName;
			var lso:SharedObject = SharedObject.getLocal("profileicons");
			lso.data[profileName] = eventObj.target.data;
			lso.flush();
			lso.close();
			lounge.currentPlayerProfile.profileData.child("icon")[0].replace("*", new XML("<![CDATA[lso://"+profileName+"]]>"));
			GlobalSettings.saveSettings();
			lounge.currentPlayerProfile.load();
		}
		
		/**
		 * Updates the profile UI with the current icon image and player handle.
		 * 
		 * @param	eventObj A LoungeEvent object.
		 */
		private function updateProfileInfo(eventObj:LoungeEvent):void {			
			this.updatePlayerIconButton.defaultIcon = lounge.currentPlayerProfile.newIconImage;
			this.updatePlayerIconButton.invalidate();
			this.playerHandleInput.text = lounge.currentPlayerProfile.profileHandle;
		}
		
		/**
		 * Event listener invoked when the "player handle" text input field loses focus causing the data entered into the field to be
		 * save to the global settings.
		 * 
		 * @param	eventObj An Event object.
		 */
		private function onPlayerHandleInputLoseFocus(eventObj:Event):void {
			lounge.currentPlayerProfile.profileData.child("handle")[0].replace("*", new XML("<![CDATA[" + this.playerHandleInput.text + "]]>"));
			GlobalSettings.saveSettings();
			lounge.currentPlayerProfile.load();
		}
	}
}